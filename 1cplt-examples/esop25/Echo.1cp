role* @node(
  init: boolean,
  neighs: @node[],
  senders: @node[] = [],
  parent?: @node,
  sender?: @node
) {
  main:
    parent := self ;
    if init then
      roll

  roll:
    foreach<@node> neigh in neighs do
      if neigh != parent then
        self -> neigh.sender |> recv

  recv:
    if sender != undefined then {
      senders := [sender].concat(senders) ;
      if !init then {
        if
          // Step 1: First receive?
          senders.length == 1
        then {
          // Step 2: Send to non-parents
          parent := sender ;
          \echo parent ;
          roll
        } ;
        if
          // Step 3: Last receive?
          senders.length == neighs.length && parent != undefined
        then
          // Step 4: Send to parent
          self -> parent.sender |> recv
      }
    }
}

process @node[1](init = true,  neighs = [          @node[2], @node[3], @node[4]])
process @node[2](init = false, neighs = [@node[1],           @node[3], @node[4]])
process @node[3](init = false, neighs = [@node[1], @node[2],           @node[4]])
process @node[4](init = false, neighs = [@node[1], @node[2], @node[3]          ])

#analyze
#compile
#execute
